# vim:ft=zsh

case $BUFFER in
  sw|'git switch')
    # ブランチを選択するときにはoriginのブランチであることを表示したいが、switchするときには"origin/"が不要なのでfzfで選択したあとにsedで削除
    local branch=$(git branch -vv -a | sed 's/remotes\/\(.*\)/\1/g' | fzf -m | sed 's/origin\/\(.*\)/\1/g' | awk '{print $1}')
    BUFFER="git switch ${branch}"
    CURSOR=${#BUFFER}
    ;;
  git\ push\ (-f|--force))
    BUFFER='git push --force-with-lease --force-if-includes'
    CURSOR=${#BUFFER}
    ;;
  add|'git add')
    BUFFER="git add $(git-status)"
    CURSOR=${#BUFFER}
    ;;
  co\ -m|'git commit -m')
    BUFFER="git commit -m '$(gitmoji) '"
    CURSOR="${#BUFFER} - 1"
    ;;
  rb\ -i|'git rebase -i')
    BUFFER="git rebase -i $(git-commit-preview | grep -oE '\b[a-f0-9]{7,8}\b')"
    CURSOR=${#BUFFER}
    ;;
  de|'docker exec')
    local container=$(docker ps --format '{{ .Names }}' | fzf -m)
    BUFFER="docker exec -it ${container} /bin/zsh"
    CURSOR=${#BUFFER}
    ;;
  'cargo new')
    BUFFER="cargo-new "
    CURSOR=${#BUFFER}
    ;;
  *)
    zle _expand_alias
    zle .self-insert
    ;;
esac

zle .reset-prompt
zle autosuggest-fetch
